string = /"[^"]+"/ ;
ident = /[a-zA-Z_][a-zA-Z_0-9]*/ ;
lit = /\d+/ ;
doccomment = "'" @:/.*/ ;

protocol = config:[config] {types+:newtype | categories+:category | includes+:include} $ ;

config = 'config' '{' {@+:valfield ','} [@+:valfield [',']] '}' ;
valfield  =  name:ident ':' value:lit ;

newtype   =  'newtype' name:ident '=' type:type ';' ;
category  =  'category' name:ident '=' code:lit '{' {includes+:include | methods+:method} '}' ;
include   =  'include' @:string ';' ;

method    =  'method' name:ident '=' code:lit '{' attrs:{ident} '}' '{' doccomments:{doccomment} properties:{property} '}' ;
property  =  name:ident '=' type:type ';' ;

type
    = @:primitive
    | obj:object
    | @:('array' '<' array:type '>')
    | @:('map' '<' key:type ',' val:type '>')
    | @:'string'
    | @:ident
    ;

primitive
    = 'i8'  | 'u8'
    | 'i16' | 'u16'
    | 'i32' | 'u32' | 'f32'
    | 'i64' | 'u64' | 'f64'
    ;

object    =  '{' {@+:field ','} [@+:field [',']] '}' ;
field     =  name:ident ':' type:type ;
