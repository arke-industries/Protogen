string = /"[^"]+"/ ;
ident = /[a-zA-Z_][a-zA-Z_0-9]*/ ;
number = /\d+/ ;
doccomment = "'" @:/.*/ ;

lit = (@:number | @:string) ;

server = 'server' name:ident ';' ;
notif = 'notification' name:ident ';' ;
resp = 'response' name:ident ';' ;

config = 'config' '{' {@+:valfield ','} [@+:valfield [',']] '}' ;
valfield  =  name:ident ':' value:lit ;

attr_inner = name:ident '=' val:(lit | ident) | name:ident '(' args:{attr_inner ','} args+:[attr_inner [',']] ')' | plain:(lit | ident);
attrs = {'[' @:attr_inner ']'} ;

type
    = @:primitive
    | obj:object
    | @:('array' '<' array:type '>')
    | @:('map' '<' key:type ',' val:type '>')
    | @:'string'
    | @:ident
    ;

primitive
    = 'i8'  | 'u8'
    | 'i16' | 'u16'
    | 'i32' | 'u32' | 'f32'
    | 'i64' | 'u64' | 'f64'
    ;

field     =  [attributes:attrs] name:ident ':' type:type ;
object    =  '{' {@+:field ','} [@+:field [',']] '}' ;

newtype   =  'type' name:ident '=' type:type ';' ;
include   =  'include' @:string ';' ;
property  =  [attributes:attrs] name:ident '=' type:type ';' ;
method    =  [attributes:attrs] 'method' name:ident ['{' attrs:{ident} '}'] '{' doccomments:{doccomment} properties:{property} '}' ;

category  =  'category' name:ident '{' {properties+:property | includes+:include | methods+:method} '}' ;
protocol = config:[config] {response_codes+:resp | notifications+:notif | servers+:server | types+:newtype | categories+:category | includes+:include} $ ;
